# Maintainer: Librewish <librewish@gmail.com>
pkgname=wayfire-plugins-extra-git
pkgver=0.8.0.20231006.c235eed
pkgrel=1
pkgdesc="3D wayland compositor extra plugins"
arch=('any')
url="https://github.com/WayfireWM/wayfire-plugins-extra"
license=('MIT')
depends=('wayfire-git' 'glibmm' 'iio-sensor-proxy' 'pango')
makedepends=('git' 'meson' 'ninja')
optdepends=('wcm: GTK3-based configuration tool for the Wayfire compositor')
provides=("${pkgname}" "wayfire-plugins-extra")
conflicts=("wayfire-plugins-extra")
replaces=()
options=(debug)
source=("${pkgname}::git+https://github.com/WayfireWM/wayfire-plugins-extra")
sha256sums=('SKIP')
install=wayfire-plugins-extra.install

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        -Denable_focus_request=true \
        -Denable_windecor=true \
        -Denable_wayfire_shadows=true \
        "${srcdir}/${pkgname}/" "$1"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".%s.%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d)" "$(git rev-parse --short HEAD)"
}


build() {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}

package() {
    DESTDIR="${pkgdir}/" meson install -C "${srcdir}/${_builddir}"
}
