# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wf-shell-git
pkgver=0.10.0.r20250113.aaadead
_pkgver=${pkgver%%.r*}
pkgrel=1
pkgdesc='GTK3-based panel for the Wayfire compositor'
url=https://wayfire.org
arch=(x86_64)
license=('MIT')
provides=("${pkgname%-git}=${_pkgver}" "${pkgname}")
conflicts=("${pkgname%-git}" "${pkgname}")
options=()
depends=(wf-config gtkmm3 gtk-layer-shell libdbusmenu-gtk3 libpulse)
makedepends=(wayland wayland-protocols meson ninja git gobject-introspection glib2-devel)
source=("${pkgname}::git+https://github.com/WayfireWM/wf-shell")
sha512sums=('SKIP')

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        -Dpulse=enabled \
        "${srcdir}/${pkgname}/" "$1"
}

prepare() {
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".r%s.%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d)" "$(git rev-parse --short HEAD)"
}


build() {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}


package() {
    DESTDIR="${pkgdir}/" meson install -C "${srcdir}/${_builddir}"
    install -Dm644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 "${pkgname}/wf-shell.ini.example" "${pkgdir}/usr/share/doc/${pkgname}/wf-shell.ini.example"
}
