# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Omar Pakker
pkgname=wlroots-lily-git
pkgver=0.17.0.20231207.070459
pkgrel=1
license=('MIT')
pkgdesc='Modular Wayland compositor library (git version, with patches)'
url='https://gitlab.freedesktop.org/lilydjwg/wlroots'
arch=('x86_64')
depends=(
    'libdisplay-info.so'
    'libliftoff.so'
    'libglvnd'
    'libinput'
    'libpixman-1.so'
    'libseat.so'
    'libudev.so'
    'libvulkan.so'
    'libwayland-client.so'
    'libwayland-server.so'
    'libxcb'
    'libxkbcommon.so'
    'opengl-driver'
    'xcb-util-errors'
    'xcb-util-renderutil'
    'xcb-util-wm'

    'xorg-xwayland')
makedepends=(
    'git'
    'glslang'
    'meson'
    'ninja'
    'systemd'
    'vulkan-headers'
    'wayland-protocols'
    'xorg-xwayland'
)
optdepends=(
    'xorg-xwayland-lily: HiDPI support, see https://gitlab.freedesktop.org/xorg/xserver/-/merge_requests/733'
)
provides=(
    'libwlroots.so'
    "wlroots=${pkgver%%.r*}"
)
conflicts=(wlroots wlroots-git)
options=(
    'debug'
)
source=("${pkgname}::git+https://gitlab.freedesktop.org/lilydjwg/wlroots.git#branch=lilydjwg")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  (
     set -o pipefail
     version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^-']*")
     printf "${version}.%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
  )
}

build() {
    arch-meson \
      -Dwerror=false \
      -Dexamples=false \
      "${pkgname}" build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 "${pkgname}/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
