# Maintainer: Solomon Choina <shlomochoina@gmail.com>
pkgname=wf-config-git
pkgver=0.10.0.r20250622.d67161d
_pkgver=${pkgver%%.r*}
pkgrel=1
pkgdesc="A library for managing configuration files, written for wayfire"
arch=('x86_64')
url="https://github.com/WayfireWM/wf-config"
license=('MIT')
depends=('glibc' 'gcc-libs' 'libevdev' 'libxml2')
makedepends=('git' 'wlroots' 'meson' 'ninja' 'wayland-protocols' 
              'glm' 'doctest' 'cmake')
provides=("${pkgname%-git}=${_pkgver}" "${pkgname}")
conflicts=("${pkgname%-git}" "${pkgname}")
replaces=()
options=(debug)
source=("${pkgname}::git+https://github.com/WayfireWM/wf-config")
sha256sums=('SKIP')

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        "${srcdir}/${pkgname}/" "$1"
}

prepare() {
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".r%s.%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d)" "$(git rev-parse --short HEAD)"
}


build() {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}


package() {
    DESTDIR="${pkgdir}/" meson install -C "${srcdir}/${_builddir}"
    install -Dm644 "${srcdir}/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
