# Maintainer: Solomon Choina <shlomochoina@gmail.com0
pkgname=wayfire-git
pkgver=0.8.0r2174.0fc949ea
pkgrel=1
pkgdesc="3D wayland compositor"
arch=('x86_64')
url="https://github.com/ammen99/wayfire"
license=('MIT')
depends=('wlroots-git' 'cairo' 'glm' 'libjpeg' 'wf-config-git' 'seatd' 'pango')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols' 'doctest' 'cmake')
optdepends=('wf-shell-git: GTK3-based panel for the Wayfire compositor'
            'wf-sound-control-git: Small utility for the Wayfire compositor to control sound volume')
provides=("${pkgname%-git}" "${pkgname%-git}")
conflicts=("${pkgname%-git}" "${pkgname%-git}")
replaces=()
options=()
source=("${pkgname}::git+https://github.com/WayfireWM/wayfire" "grabkey.patch")
sha256sums=('SKIP' '2e20082a7e7a0c6b1140c04de9a939c253f307560f3d7a6e58092c400524abc2')

pkgver () {
    cd "${srcdir}/${pkgname}"
    (
        set -o pipefail
        version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^']*")
        printf "${version}r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 < "${srcdir}/grabkey.patch"
}

build() {
    arch-meson \
        -Duse_system_wlroots=enabled \
        -Duse_system_wfconfig=enabled \
        -Dtests=disabled \
        "${srcdir}/${pkgname}/" \
        build
    ninja -C build
}


package() {
    DESTDIR="${pkgdir}/" ninja -C build install
    install -m644 "${srcdir}/${pkgname}/wayfire.ini" "${pkgdir}/usr/share"
}
