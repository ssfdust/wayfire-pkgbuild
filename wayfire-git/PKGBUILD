# Maintainer: Solomon Choina <shlomochoina@gmail.com0
pkgname=wayfire-git
pkgver=0.8.0.r2372.d296170e
pkgrel=1
pkgdesc="3D wayland compositor"
arch=('x86_64')
url="https://github.com/ammen99/wayfire"
license=('MIT')
depends=('wlroots-git' 'cairo' 'glm' 'libjpeg' 'wf-config-git' 'seatd' 'pango')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols' 'doctest' 'cmake' 'nlohmann-json')
optdepends=('wf-shell-git: GTK3-based panel for the Wayfire compositor'
            'wf-sound-control-git: Small utility for the Wayfire compositor to control sound volume')
provides=("${pkgname%-git}" "${pkgname%-git}")
conflicts=("${pkgname%-git}" "${pkgname%-git}")
replaces=()
options=(debug)
source=("${pkgname}::git+https://github.com/WayfireWM/wayfire#commit=f2b65ef3870dc13d57fccff3394cc9dc4a9eea7e")
sha256sums=('SKIP')

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        -Duse_system_wlroots=enabled \
        -Duse_system_wfconfig=enabled \
        -Dtests=disabled \
        "${srcdir}/${pkgname}/" "$1"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}


package() {
    DESTDIR="${pkgdir}/" meson install -C "${srcdir}/${_builddir}"
    install -m644 "${srcdir}/${pkgname}/wayfire.ini" "${pkgdir}/usr/share"
}
