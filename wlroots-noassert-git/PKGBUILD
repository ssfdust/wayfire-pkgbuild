# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
# Maintainer: Antonin DÃ©cimo <antonin dot decimo at gmail dot com>
pkgname=wlroots-noassert-git
pkgver=0.15.1.r5322.29938b74
pkgrel=1
license=(custom:MIT)
pkgdesc='Modular Wayland compositor library (git version)'
url=https://gitlab.freedesktop.org/wlroots/wlroots
arch=(x86_64)
provides=("libwlroots.so" "wlroots=${pkgver%%.r*}" "wlroots-git")
conflicts=(wlroots wlroots-git)
options=(debug)
depends=(
    glslang
    libinput
    libxcb
    libxkbcommon
    opengl-driver
    pixman
    wayland
    xcb-util-errors
    xcb-util-renderutil
    xcb-util-wm
    seatd
    vulkan-icd-loader
    vulkan-validation-layers
    xorg-xwayland)
makedepends=(
    git
    meson
    vulkan-headers
    wayland-protocols
    xorgproto)
source=("${pkgname}::git+${url}#commit=29938b74251e826f3778f6bf9c54974a30488cc1" "noassert.patch")
sha256sums=('SKIP' '6910365f5c01d9fd7b6fe817dd174b83d9a0bf16a67e7bffd7feb1acdbd700b9')

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        --buildtype=debug \
        -Dwerror=false \
        -Dexamples=false \
        "${srcdir}/${pkgname}" "$1"
}

prepare () {
    cd "${srcdir}/${pkgname}"
    patch -p1 < "${srcdir}/noassert.patch"
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${srcdir}/${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build () {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}

package () {
    DESTDIR="${pkgdir}" meson install -C "${srcdir}/${_builddir}"
    install -Dm644 "${pkgname}/"LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

post_upgrade() {
    echo "Make sure to upgrade wlroots-git and sway-git together."
    echo "Upgrading one but not the other is unsupported."
}
