# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wcm-git
pkgver=0.8.0.r239.2b139dc
pkgrel=1
pkgdesc='Wayfire Config Manager'
url=https://wayfire.org
arch=(x86_64)
license=(custom:MIT)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
depends=(gtkmm3 wayfire-git wf-shell-git)
options=(debug)
makedepends=(wayland-protocols meson ninja git extra-cmake-modules)
optdepends=("wayfire-plugins-extra: configuration for extra Wayfire plugins")
source=("${pkgname}::git+https://github.com/WayfireWM/wcm")
sha512sums=('SKIP')

_builddir="build"
_builddir_pkgver="build-pkgver"

_meson_setup () {
    arch-meson \
        "${srcdir}/${pkgname}/" "$1"
}

prepare() {
    _meson_setup "${srcdir}/${_builddir_pkgver}"
}

pkgver () {
    (
        set -o pipefail
        meson introspect --projectinfo "${_builddir_pkgver}" \
            | awk 'match($0, /"version":\s*"([[:digit:]]+\.[[:digit:]]+\.[[:digit:]]+)"/, ret) {printf "%s",ret[1]}'
    )
    cd "${srcdir}/${pkgname}"
    printf ".r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
    _meson_setup "${srcdir}/${_builddir}"
    meson compile -C "${srcdir}/${_builddir}"
}


package() {
    DESTDIR="${pkgdir}/" meson install -C "${srcdir}/${_builddir}"
}
